@top Document { Note* }

Note {
  NoteDelimiter NoteContent
}

NoteDelimiter {
    noteDelimiterEnter noteDelimiterMark NoteLanguage Auto? NoteCreated? NoteUpdated? noteDelimiterEnter
}

NoteCreated { 
    noteCreatedDelimiter time
}

NoteUpdated { 
    noteUpdatedDelimiter time
}

@tokens {
    noteDelimiterMark { "∞∞∞" }
    NoteLanguage { "text" | "math" | "javascript" | "typescript" | "jsx" | "tsx" | "json" | "python" | "html" | "sql" | "markdown" | "java" | "php" | "css" | "xml" | "cpp" | "rust" | "csharp" | "ruby" | "shell" | "yaml" | "golang" | "clojure" | "erlang" | "lezer" | "toml" | "swift" | "kotlin" }
    Auto { "-a" }
    noteDelimiterEnter { "\n" }
    noteCreatedDelimiter { "-c" }
    noteUpdatedDelimiter { "-u" }
    time { 
        @digit @digit @digit @digit "-" @digit @digit "-" @digit @digit "T" 
        @digit @digit ":" @digit @digit ":" @digit @digit "Z"
    }
    //NoteContent { String }
    //String { (![∞])+ }
}

@external tokens noteContent from "./external-tokens.js" { NoteContent }

